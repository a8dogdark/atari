PAG7
INITPAG7    = $2000
LOGOPAG7	= [[PAGINA7.LOGOPAG7 - PAGINA7] + PAG7]
TITULOPAG7  = [[PAGINA7.TITULOPAG7 - PAGINA7] + PAG7]
;SAVE#D:PAGINA7.ASM
;*=  $0700 $06FE
LARGOPAG7 = [.LEN PAGINA7]
.PROC PAGINA7,INITPAG7
INICIOPAG7
    .BYTE $00
    .BYTE [LARGOPAG7+127]/128
    .WORD INICIOPAG7,PANTALLAPAG7
    JSR ROMRAMPAG7
    LDX $0230   ;SDLSTL
    LDY $0231   ;SDLSTH
    STX $CFFC
    STY $CFFD
    LDX # <DLSPAG7
    LDY # >DLSPAG7
    LDA #$00
    STX $0230   ;SDLSTL
    STX $D402   ;DLISTL
    STY $0231   ;SDLSTH
    STY $D403   ;DLISTH
    STA $41     ;SOUNDR
    JMP $0400
ROMRAMPAG7
    LDX #$C0
    LDY #$00
    SEI 
    LDA $D40E   ;NMIEN
    PHA 
    STY $D40E   ;NMIEN
    STX $CC
    STY $CB
LOOPPAG7
    LDA ($CB),Y
    DEC $D301   ;PORTB
    STA ($CB),Y
    INC $D301   ;PORTB
    INY 
    BNE LOOPPAG7
    INC $CC
    BEQ EXITPAG7
    LDA $CC
    CMP #$D0
    BNE LOOPPAG7
    LDA #$D8
    STA $CC
    BNE LOOPPAG7
EXITPAG7
    DEC $D301   ;PORTB
    LDX #$01
    LDY #$4C
    LDA #$13
    STX $EE17
    STY $ED8F
    STA $ED67
    LDX #$80
    LDY #$03
    STX $EBA3
    STY $EBA8
    LDY #$04
    LDA #$EA
NOPPAG7
    STA $ED85,Y
    DEY 
    BPL NOPPAG7
    LDY #STACFPAG7-STACIPAG7
MOVEPAG7
    LDA STACIPAG7,Y
    STA $ECEF,Y
    DEY 
    BPL MOVEPAG7
    PLA 
    STA $D40E   ;NMIEN
    CLI 
    CLC 
    RTS 
STACIPAG7
    LDA #$7D
    LDX $62     ;PALNTS
    BEQ CC0PAG7
    LDA #$64
CC0PAG7
    CLC 
    ADC $EE19,X
    DEY 
    BPL CC0PAG7
    CLC 
    ADC $0312   ;TEMP1
    SEC 
    SBC #$64
    BCC CC3PAG7
    STA $0312   ;TEMP1
    LDY #$02
    LDX #$06
    LDA #$4F
CC2PAG7
    ADC $0312   ;TEMP1
    BCC CC1PAG7
    INY 
    CLC 
CC1PAG7
    DEX 
    BNE CC2PAG7
    STA $02EE   ;CBAUDL
    STY $02EF   ;CBAULH
    JMP $ED96
CC3PAG7
    JMP $ED3D
STACFPAG7
    .BYTE "DOGDARK SOFTWARE 2020"
DLSPAG7
    .BYTE $70,$70,$70,$00,$70,$70,$70,$70
    .BYTE $70,$70,$70,$70,$70,$46
    .WORD LOGOPAG7
    .BYTE $70,$06,$41
    .WORD DLSPAG7
LOGOPAG7
    .SB "                    "
TITULOPAG7
    .SB "                    "
PANTALLAPAG7
    LDX #$00
    LDY #$07
    STX $0C     ;DOSINI
    STY $0D     ;DOSINI+1
    LDX $CFFC
    LDY $CFFD
    STX $0230   ;SDLSTL
    STY $0231   ;SDLSTH
    JMP ($304)   ;DBUFLO
    .BYTE "DOGDARK 2025"
FINPAG7
.ENDP